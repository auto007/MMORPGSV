%200
;;------------------------------------------------------------------------------
;; [310] 找老人(接到任务的角色)
;; [311] 找鹿血和鹿茸
;; [312] 找到鹿血和鹿茸的人
;;------------------------------------------------------------------------------
[@main]
#IF 
random 2
#ACT
goto @QUEST
#ELSEACT
goto @normal


[@QUEST]
#IF 
check [312] 1
#ACT
goto @Q312
break


#IF
check [311] 1
#ACT
goto @Q311
break


#IF
check [351] 1
#ACT
goto @Q351
break


#IF
check [310] 1
random 5
#ACT 
goto @Q310
#ELSEACT
goto @normal


[@normal]
嘿！这么危险的地方你来干吗？？？\
不知道你能不能走出这个地方...\ \
 <对话/@talkwith_1>\
 <退出/@exit>\


[@Q310]
#IF
#SAY
你找我有什么事情吗？？是不是让我帮你制作神秘水？？\ 
制作这种水是需要特殊材料制作的，\
如果你收集到这些材料的话我就帮你制作。\ 
制作神秘水的特殊材料是鹿茸和鹿血，\
如果你在其他地方找不到这种材料的话，我可以告诉你...\ \
 <继续/@Q310_1>\


[@Q310_1]
#IF
#ACT
SET [311] 1
#SAY
鹿茸可以在未知暗殿找到，\
你知道前往未知暗殿的路吗？？如果你不知道那就没办法了。\ 
关于鹿血...我也不清楚在那里可以得到。\ 
祝你好运！\ \
 <离开/@exit>\


[@Q311]
#IF
check [311] 1
checkitem 鹿茸 1
checkitem 鹿血 1
#ACT
goto @Q311_1
#ELSEACT
goto @Q311_2


[@Q311_1]
#IF
#ACT
SET [312] 1
take 鹿血 1
take 鹿茸 1
#SAY
你真厉害！这么快就找到了鹿茸和鹿血，\
我给你制作神秘水以后，交给比奇城买肉的屠夫金氏。\
快去吧！\ \
 <离开/@exit>\


[@Q311_2]
#IF
#SAY
还没有找到吧？尽量快一点，\
因为我没有多少耐心，等不了那么长的时间。\ \
 <离开/@exit>\


[@Q312]
#IF
#SAY
快点去找屠夫金氏吧。\ \
 <离开/@exit>\


[@talkwith_1]
#IF
dayofweek Sunday
hour 20 20
#ACT
goto @talkwith_2
break


#IF
dayofweek Saturday
hour 20 20
#ACT
goto @talkwith_2
#ELSEACT
goto @untalkwith
   

[@talkwith_2]
你既然来这里找我，那我就告诉你个秘密，\
这附近有一个无人知晓的山洞，山洞里的世界跟传奇的世\
界是不同的，我也只是听说过而已。如果想进去的话，你 \
需要一个凭证，找到了凭证之后，你就可以进去试一下了。\ \
 <挑战/@talkwith_3>\
 <离开/@exit>\


[@talkwith_3]
#IF
checkitem 山洞凭证 1
#SAY
你已经得到了凭证我可以让你进去，但只能在里面待3个小时。\
自己多加小心！\ \
 <进入山洞/@talkwith_4>\
#ACT
take 山洞凭证 1
#ELSESAY 
对不起。你必须拥有山洞凭证才能进入。\
先去找进入山洞的凭证吧！\ \
 <离开/@exit>\
   

[@talkwith_4]
#IF
checkhum M001 200
#SAY
我很想带你进去，但我现在已经筋疲力尽了，\
所以我不能把你带过去...稍微等一会吧。\ \
 <离开/@exit>\
#ELSEACT
TimeRecall 180
map M001


[@Q351]
#IF
random 5
#ACT
goto @Q351_1
#ELSEACT
goto @normal


[@Q351_1]
#IF
#SAY
你就是传说中性格最怪僻的人？？\
跟我的性格差不多...我喜欢你这个人，\
你想不想赚一大笔钱？？\ \
 <想/@Q351_1_1>\
 <不想/@Q351_1_2>\


[@Q351_1_1]
你真的很想赚一笔吗？？\
那你带着斩马刀去找奸商崔氏，\
他会帮你的。\ \
 <离开/@Q351_1_1_1>\


[@Q351_1_2]
你的性格看起来不怪僻嘛，\
很少有像你这样性格的人，\
再去积累点修养吧。\ \
 <离开/@exit>\


[@Q351_1_1_1]
#IF
#ACT
SET [352] 1
#SAY 
祝你好运！\ \
 <离开/@exit>


[@untalkwith]
#IF 
random 10
#SAY 
你听说过未知暗殿吗？？\
关于未知暗殿我现在不能告诉你太多。\
进入未知暗殿的时间是周末的傍晚，\
到那个时候再来找我，\
我再带你过去。\ 
到时候见！\ \
 <返回/@Main>\
#ELSESAY
我不想跟你废话,\
离我远点! \ \
 <返回/@Main>\
