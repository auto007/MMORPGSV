(@sell)
%100
+40
+1
;;------------------------------------------------------------------------------
;; [301] 拒绝请求的人
;; [302] 接收请求的人
;; [303] 完全忽略请求的人
;; [304] 答应请求，并拿来肉块时
;;------------------------------------------------------------------------------
[@main]
#IF
checkpkpoint 2
#SAY
我不想和你这样的人打交道！\ \
 <离开/@exit>\
#ELSEACT
goto @main-1


[@main-1]
#IF
random 2
#ACT
goto @QUEST
#ELSEACT
goto @normal


[@normal]
#IF
check [303] 1
#SAY
我根本就不认识你！\ 
快走开，一看到你的脸，我就感觉恶心...\
还不快走！\ \
 <离开/@exit>\


#ELSESAY
是来卖肉吗？\ \
 <卖/@sell>肉\
 <离开/@exit>\

   
[@QUEST]
#IF
check [303] 1
#SAY
我根本就不认识你！\ 
快走开，一看到你的脸，我就感觉恶心...\
还不快走！\ \
 <离开/@exit>\


#IF
check [313] 1
#ACT
goto @Q313
break


#IF 
check [304] 1
#ACT
goto @Q304
break


#IF
check [302] 1
#ACT
goto @Q302
break


#IF
check [301] 1
#ACT
goto @Q301
break


#IF
check [301] 0
check [302] 0
check [303] 0
#ACT
goto @Q300
#ELSEACT
goto @normal


[@Q301]
#IF
random 5
#SAY 
你能理解我的心情吗？我实在是太急了所以才请求你。 \
但是你却拒绝我的请求，太过分了。\
再一次请求你，好吗？？ \ \
 <接受/@Q300_2_1>\
 <拒绝/@Q301_1>\
#ELSESAY
不想和你这种人再见面，你走吧! \ \
 <离开/@exit>\


[@Q300]
#IF
#SAY
你想和我说话吗？？ 嗯...\
或许你会觉得这只是我个人的事情...\
但是可以听我讲一次吗？？ \ \
 <接受/@Q300_1>\
 <拒绝/@exit>\


[@Q302]
#IF
checkitem 肉 10
checkitem 鸡肉 10
checkduraeva 肉 20
checkduraeva 鸡肉 5
#ACT
goto @Q302_1
#ELSEACT
goto @Q302_2


[@Q302_1]
#IF
#ACT
take 肉 10
take 鸡肉 10
give 金币 6000
SET [304] 1
#SAY
谢谢你..如果我富有，就给你算得多一点，\
但是我也很穷，所以只能肉500金币/块，鸡肉200金币/块来算。\
真不好意思！\
你去找一下比奇省的屠夫金氏吧！\
其实，他说有话要和你讲。\ 
 <离开/@exit>\


[@Q302_2]
#IF
#SAY
你都没有准备好，怎么还对我这样？\
请快一点救救我! \ \ 
 <离开/@exit>


[@Q304]
#IF
check [304] 1
#SAY
快去找比奇省的屠夫金氏！\ \
 <离开/@exit>\


[@Q300_1]
我有一个老朋友。\
他就是住在比奇省的叫做石禹的人，\
据我所知，他现在正匆忙赶来找我。\
这位朋友嘴很刁，一般的食物连看都不看。\
 <继续/@Q300_2>\


[@Q300_2]
所以说你能替我找来肉吗？\
我也想去找，但是我过日子也不容易，不能不做买卖,\
所以想请你帮个忙。\ \
 <好的/@Q300_2_1>\
 <拒绝/@Q300_2_2>\


[@Q300_2_1]
#IF
#ACT
SET [302] 1
SET [301] 0
SET [303] 0
#SAY
很感谢你答应我的请求。 \
如果可以的话，请尽快把肉拿来。\
哦，差一点忘了告诉你，\
我需要的是品质20以上的肉和品质5以上的鸡肉，\
一共是10块肉和10块鸡肉。\ \
 <离开/@exit>\


;; 第一次拒绝请求时
[@Q300_2_2]
#IF
#ACT
SET [301] 1
#SAY
我都跟你讲了我的难处，你还拒绝我，真过分！\
我再也不想见到你，太让我失望了，\
快在我面前消失！\ \
 <离开/@exit>\


[@Q301_1]
#IF
#SAY
我这样求你，你都不答应...好，\ 
你这种人我可是第一次见，\ 
你就自己去发大财吧，\
我不想再见到你了。\ 
(在如今社会，这些可是个问题。)\ \
 <离开/@exit>\
#ACT
SET [303] 1


[@Q313] 
#IF
#ACT
SET [314] 1
#SAY
哦，你准备得很好，多亏了你，\
我可以好好招待我那位叫做石禹的朋友。\
我拜托我的老朋友林小姐，为你准备了一样好东西，\
你去找一下林小姐吧。\
真的很感谢你。\
(在如今社会，这些可是个问题。)\ \
 <离开/@exit>\


;;卖肉时
[@sell]
我会用最高的价格购买高品质的肉。\
但是如果沾上泥土，或者被火焚烧过的，\
那我就只能用低廉的价格收购了。\ \
 <返回/@main>\


[goods]
;商品名  数量  时间(hour)
