(@buy @sell)
%100
+0
+42

[@main]
#IF
checkpkpoint 2
#SAY
我不想和你这样的人打交道！\ \
 <离开/@exit>\
#ELSEACT
goto @main-1


[@main-1]
欢迎光临，我可以为你些做什么？\ \
 <购买/@buy>药品\
 <出售/@sell>药品\
 <对话/@talkwith>\
 <离开/@exit>\


[@buy]
你需要些什么？\ \ \
 <返回/@main>\


[@sell]
请把要出售的物品放在这里。\ \ \
 <返回/@main>\


;#####################################################


[@talkwith]
#IF
check  [1] 0
#ACT
goto  @talkwith_1


;#####################################################


[@talkwith_1]
#IF
check [103] 1
check [104] 0
check [105] 0
#ACT
goto @talk_about_letter
break

#IF
check  [103]  1
check  [104]  1
check  [106]  0
#ACT
goto @talk_1_1
break

#IF
check  [104]  1
check  [106]  1
check  [107]  0
#ACT
goto @talk_2_1
break

#IF
check  [104]  1
check  [106]  1
check  [107]  1
check  [108]  0
#ACT
goto 	@talk_3_1
break

#IF
check  [105] 1
#SAY
<$USERNAME>对不起，我现在太忙。\
可不可以下次再来？\
很抱歉。\
#ACT
break

#IF
#SAY
对不起，我现在忙，以后再来找我吧。\


[@talk_about_letter]
...就是说你带着我父亲的信件，\
因为什么事情给我写信的呢？\
快把信交给我。\ \
 <传达信件/@give_letter>\
 <信件丢了/@no_give_letter>\


[@give_letter]
#IF
CHECKITEM  药剂师的信  1
#ACT
TAKE  药剂师的信  1
SET   [104]   1
#SAY
哦，谢谢你作了这件麻烦的事，\
你叫什么名字？\ \
 <告诉他名字/@say_my_name>\
#ELSESAY
现在没有信？\
你是说在路上丢了？找到之后马上给我。\


[@no_give_letter]
什么？把信丢了？\
呵呵，好了，那也用不着这么客气，\
这样一来我就变得不好意思了，\
好了，没事了，你回去吧。\


[@say_my_name]
<$USERNAME>谢谢你，让你辛苦了。\
这也是缘分，我们友好相处吧。\
这是我的一点心意，收下吧。\
是叫做强效太阳水的药水，很管用的。\ \
 <收下/@ok_take_drug>\


[@ok_take_drug]
#ACT
SET  [105]    1
GIVE 强效太阳水 1
#SAY
以后有时间再来这里找我吧!\
再见...\


[@talk_1_1]
#SAY
啊<$USERNAME>，欢迎光临！\
你过的还好吗？\
现在我需要食人树果实，\
如果你帮我弄到几个，我会给你好价钱的。\
请你一定帮我。\
那我就等你的好消息了。\
#ACT
SET [106] 1


[@talk_2_1]
#IF
CHECKITEM  食人树果实  1
#SAY
还是你好，没有忘记。\
拿来的话，就一个一个给吧。\ \
 <给/@give_sg_1>\
#ELSEACT
GOTO  @talk_1_1


[@give_sg_1]
#IF
CHECKITEM  食人树果实 1
#ACT
TAKE 食人树果实 1
GIVE 金币 600
GOTO @check_enough_1


;侥牢檬 凯概甫 面盒洒 罐疽绰瘤 八荤
[@check_enough_1]
#IF
RANDOM 10
DAYTIME SUNSET
#ACT
SET  [107] 1
#SAY
谢谢你<$USERNAME>现在食人树果实已经够了。\
这都是你的功劳。\
现在我要开始炼药了，\
请你为我祈祷吧。\
再见！\
#ELSEACT
GOTO @give_sg_2


[@give_sg_2]
#IF
CHECKITEM 食人树果实 1
#ACT
GOTO @talk_2_1
#ELSEACT
CLOSE


;-------------------------------------------------------
;倾狼盔捞 1锅掳 距阑 父甸绊 乐澜
;-------------------------------------------------------


[@talk_3_1]
#if
daytime day
#act
goto @talk_3_2
#elsesay
<$USERNAME>，欢迎光临，我的药还没有炼成。\
要一旦炼成了，请你一定答应我一个请求，\
但愿快一点把药炼成。\


[@talk_3_2]
<$USERNAME>，药是已经炼成了，\
但是还有部分问题没有解决，\
看来需要向师傅请教了。\
有件事情想求你帮忙，可以答应我吗？\ \
 <接受请求/@ok_h_order>\


[@ok_h_order]
谢谢你，你把这个药送给我师傅刘中医，\
请教师傅这剂药缺少什么东西。\
我不能空着药房，\
所以就请你辛苦一次了。\
但是我师傅好久都没有音信了，\
很对不起，请你一定帮我这个忙。\
 <接受请求/@ok_h_order_now>\


[@ok_h_order_now]
#act
set  [108] 1
give 许中医的药 1
close

;-------------------------------------------------------
;
;-------------------------------------------------------

[goods]
;items(cab be Zen)      pieces      hour
金创药(小量)		200	1	
魔法药(小量)		200	1
金创药(中量)		200	1
魔法药(中量)		200	1
强效金创药		200	1
强效魔法药		200	1
超级金创药		200	1
超级魔法药		200	1
金创药(中)包		200	1
魔法药(中)包		200	1
金创药(小)包		200	1
魔法药(小)包		200	1
