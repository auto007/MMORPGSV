(@buy @sell @repair)
%100
+10
+11
+15

[@main]
#IF
checkpkpoint 2
#SAY
我不想和你这样的人打交道！\ \
 <离开/@exit>\
#ELSEACT
goto @main-1


[@main-1]
欢迎光临，有什么可以效劳的？\ \
 <购买/@buy>衣服\
 <出售/@sell>衣服\
 <修补/@repair>衣服\
 <对话/@talkwith>\
 <离开/@exit>\


[@Tmove]
#IF
random 4
#ACT
mapmove 0 318 303
break


#IF
random 4
#ACT
mapmove 0 318 302
break


#IF
random 4
#ACT
mapmove 0 307 312
break


#IF
random 4
#ACT
mapmove 0 308 311
break
#ELSEACT
goto @Tmove


[@buy]
你需要买些什么？\ \ \
 <返回/@main>\


[@sell]
请放上去要卖的衣服，我给估价。\
头盔和帽子也收购，\
最好在这儿一起卖吧。\ \
 <返回/@main>\


[@repair]
可以修复衣服，头盔，帽子。\ \ \
 <返回/@main>\


[~@repair]
修得还算不错。\ \ \
 <返回/@main>\



;#####################################################
;[109] 请告诉我刘老头的位置。
;[110] 没有和安家布衣约定
;[111] 抢回给过的金项链
;[112] 与安家布衣亲密(男子时)
;[113] 与安家布衣很亲密
;[114] 开始安家布衣悲伤的故事


[@talkwith]
#IF
check [1] 0
#ACT
goto  @talkwith_1


;#####################################################


[@talkwith_1]
#IF
check [114] 1
#ACT
goto  @eppuni_sad_story
break


#IF
check [112] 1
#ACT
goto  @lover_eppuni
break
 

#IF
check [108] 1
check [109] 0
#ACT
goto  @talk_eppuni
break


#IF
daytime  day
#SAY
今天天气真好！\
这么好的天气，\
就该和男朋友一起出去玩。\
#ACT
break


#IF
#SAY
天黑路上小心点，\
最近夜晚有强盗在路上胡闹，\
要尽快把这些强盗消灭掉。\


;-----------------------------------------------------


[@talk_eppuni]
#SAY
有什么事情吗？我现在要做事，\
简单点问吧。\ \
 <问有关刘老中医的事情/@talk_yu_doctor>\


[@talk_yu_doctor]
#IF
checkitemw  金项链 1
#SAY
哦，原来是在讲刘老头啊，\
可是我答应过他对任何人都不说他在哪儿。\ \
 <给身上带着的金项链/@give_Gold_necklace>\
#ELSESAY
哦，原来是在讲刘老头啊。\
可是我答应过他对任何人都不说他在哪儿。\ 
很抱歉，我也没办法，\
你还是回去吧。\


[@give_Gold_necklace]
#IF
checkitemw  金项链 1
#SAY
哦，给我这么贵重的东西，太感谢了，\
真太谢谢了，\
这是我梦寐以求的东西。\ \
 <再次问有关刘中医的事情/@talk_yu_again>\
#ACT
takew  金项链 1


[@talk_yu_again]
#IF
random 5
#SAY
这可怎么办？我都已经收了礼物，\
又不能装作不知道，我还是告诉你吧，\
不过你千万不要和刘爷爷讲是我说的，\
请你一定答应我。\ \
 <答应/@ok_make_promise>\
 <拒绝/@no_make_promise>\
#ELSESAY
不行，不能说的。\
给我项链，不会是因为这个吧？\ \
 <要求退还项链/@getback_necklace>\
 <怎么会呢/@noproblem>\


[@getback_necklace]
#SAY
哼，你太低级了！\
接着，以后别再出现在这里。\
哼！
#ACT
give 金项链 1
set  [109]  1
set  [111]  1


[@noproblem]
#IF
gender man
random 20
#SAY
<$USERNAME>你是真正的男子汉！\
以后就叫你哥哥，可以这样叫吗？\
还有啊，和哥哥在一起就感觉很开心，\
下次经过这里，一定来看我喔，\
好吗？\


#ACT
set  [112] 1
#ELSESAY
你真是个好人，\
经过附近一定要来我们店喔。\


[@ok_make_promise]
#SAY
一定要这样做，\
刘爷爷现在在自然洞穴地下，\
我没有去过，所以具体就不知道了。\
走的时候说要在那里住几个月，\
我想应该在那里吧。\
就算见到，\
千万不要提起是我讲的呀！\
#ACT
set  [109] 1


[@no_make_promise]
哦，这可不行...\
我可是和刘爷爷发誓过的,\
如果他知道我没有守信用，\
会很失望的...\
对不起。\ \
 <讲许中医的请求/@talk_her_order>\


[@talk_her_order]
#SAY
原来是这样。\
既然是许中医讲的话，就应该早说嘛。\
我告诉你，\
说是在天然洞穴地下，\
你可以到那儿去找他，具体的我也不知道...\
但愿你找到他。\
#ACT
set  [109]  1
set  [110]  1


;-----------------------------------------------------


[@lover_eppuni]
#IF
check [109] 0
#SAY
<$USERNAME>哥哥，这次来又有什么事情？\
只要能帮上哥哥的忙，什么事情都可以做。\ \
 <问有关刘老头的事情/@about_yu_2>\
#ACT
break


#IF
random 5
#SAY
<$USERNAME>哥哥，我现在很疲劳...\
真对不起，我没有办法听你讲。\ \
 <把身上带着的项链当作礼物送/@give_my_necklace>\
#ACT
break


#IF
random 5
check [113] 1
#SAY
<$USERNAME>哥哥，我现在很疲劳...\
真对不起，我没有办法听你讲。\ \
 <把身上带着的项链当作礼物送/@give_my_necklace>\
 <把身上带着的戒指当作礼物送/@give_my_ring>\
#ACT
break


#IF
#SAY
<$USERNAME>哥哥，你来得正好，我正觉得无聊。\
给我讲有趣的故事吧，\
好吗？\ \
 <讲沃玛打饱嗝的故事/@talk_about_oma>\
#ACT
break


[@about_yu_2]
因为你是哥哥，所以才告诉你，\
刘爷爷在天然洞穴地下。\
我答应过他对任何人都不讲，\
哥哥也要答应我对谁都不能说。\


[@talk_about_oma]
哈哈哈哈哈...\
太有意思了，哥哥真是一个有趣的人。\


[@give_my_necklace]
#IF
checkitemw  [NECKLACE]
#ACT
takew  [NECKLACE]
goto  @thanks_neckl
#ELSESAY
哥哥也真会说笑话,\
不过有这句话，我就已经很感谢了...\


[@thanks_neckl]
#IF
random 5
#ACT
set [113] 1
#SAY
哥哥，你竟然给我这样的礼物，太好了！\
我好像又有力气了。\
#ELSESAY
哥哥，你竟然给我这样的礼物，太好了！\
我好像又有力气了。\



[@give_my_ring]
#IF
checkitemw  [RING]
#ACT
takew [RING]
#ELSESAY
哥哥也真会说笑话，\
不过有这句话，我就已经很感谢了...\


#IF
istakeitem 骷髅戒指
#ACT
goto  @eppuni_stroy_1
#ELSEACT
goto  @thanks_ring


[@thanks_ring]
真的吗？真的把这个给我吗？\
哥哥，亲你一下...\


[@eppuni_stroy_1]
#SAY
呜呜呜呜呜...\
..............................\
真谢谢你.. 把这么好的戒指给我...\
...........\
对不起。突然身体不舒服...\
#ACT
set [114] 1


;-----------------------------------------------------
;服装店女售货员的内情...


[@eppuni_sad_story]
哥哥，今天不怎么想说话。\
对不起...下次再来吧...\


;-----------------------------------------------------

[goods]
;Items (can be Zen)     Amount  hour
布衣(男)		100	1	
布衣(女)		100	1
轻型盔甲(男)		100	1
轻型盔甲(女)		100	1
重盔甲(男)		100	1
重盔甲(女)		100	1
灵魂战衣(男)		100	1
灵魂战衣(女)		100	1
魔法长袍(男)		100	1
魔法长袍(女)		100	1
青铜头盔		100	1
魔法头盔		100	1
